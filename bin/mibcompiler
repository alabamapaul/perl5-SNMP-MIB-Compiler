#!/usr/bin/perl -w

use strict;
use File::Basename qw(dirname);
use File::Spec;
## Add script directory
use lib File::Spec->catdir(File::Spec->splitdir(dirname(__FILE__)));
## Add script directory/lib
use lib File::Spec->catdir(File::Spec->splitdir(dirname(__FILE__)), qq{lib});
## Add script directory/../lib
use lib File::Spec->catdir(File::Spec->splitdir(dirname(__FILE__)), qq{..},
  qq{lib});
use SNMP::MIB::Compiler;
use Data::Dumper;
use Getopt::Long::Descriptive qw(describe_options prog_name);


my $VERSION = qq{0.001};

my ($options, $usage) = describe_options(
  qq{%c %o <MIB Filename>},
  [ qq{source|s=s@} => qq{Source path to search for MIB files}, {default => [qw(mibs)]} ],
  [ qq{out|o=s}     => qq{Output path for compiled MIB files},  {default => qq{out}} ],
  [ qq{ext|x=s@}    => qq{Extension for raw MIB files},         {default => [qq{}, qw(.mib .my)]} ],
  [ qq{debug|d+}    => qq{Debug level},                         {default => 0} ],
  [ qq{help}        => qq{Display usage and exit},],
  [ qq{version|v}   => qq{Display version and exit},],
);

## Print usage and exit
print($usage->text), exit if($options->help);

## Print version and exit
print(prog_name(), qq{ v}, $VERSION, qq{\n}), exit if ($options->version);

my $file = shift;
print(qq{ERROR: Missing MIB filename!\n}, $usage->text), exit(-1) unless ($file);

$Data::Dumper::Purity = 1;
$Data::Dumper::Indent = 1;
$Data::Dumper::Terse  = 1;

my $mib = new SNMP::MIB::Compiler;
## Add paths
$mib->add_path($_) foreach (@{$options->source});

## Add extensions
$mib->add_extension($_) foreach (@{$options->ext});

## Display values
if ($options->debug)
{
  print(qq{Source paths: "}, join(qq{", "}, @{$options->source}), qq{"\n});
  print(qq{Extensions:   "}, join(qq{", "}, @{$options->ext}), qq{"\n});
}

my $outdir = $options->out;
mkdir $outdir, oct 755 unless -d $outdir;
$mib->repository($outdir);

$mib->{'accept_smiv1'} = 1;
$mib->{'accept_smiv2'} = 1;

$mib->{'debug_recursive'} = 1;
$mib->{'debug_lexer'}     = ($options->debug >= 3 ? 1 : 0);
$mib->{'debug_load'}      = ($options->debug >= 2 ? 1 : 0);

$mib->{'make_dump'}  = 1;
$mib->{'use_dump'}   = 1;
$mib->{'do_imports'} = 1;

$mib->{'allow_underscore'} = 1;

$mib->compile($file) unless ($mib->load($file));

# my $node = 'snmpEnableAuthenTraps';
# my $oid = $mib->resolve_oid($node);
# print "$node => $oid\n";
# print "$oid => ", $mib->convert_oid($oid), "\n\n";

# $mib->load('SNMPv2-SMI');

# print $mib->resolve_oid('ifInOctets'), "\n";
# print $mib->convert_oid('1.3.6.1.2.1.31.1.1.1.10'), "\n";
# print $mib->tree('ifMIB');
